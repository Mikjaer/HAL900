#!/usr/bin/perl
use strict;
use warnings;
use FindBin qw($Bin);
use HTML::Entities;
use Email::Valid;
use Digest::SHA qw(sha1_hex);
use utf8;
use Data::Dumper;
use Text::Table;
use POSIX;

use lib "$Bin/../pm";

use HAL;
use HAL::DB;
use HAL::Util;
use HAL::Email;

my $db = new HAL::DB;

my $FROM = 'HAL på vegne af OSAA <hal@osaa.dk>';

my $mr = $db->sql("select id, email, realname from member") or die "Urgh";
while (my ($member_id, $email, $realname) = $mr->fetchrow_array) {
    
    my $accounts = '';
    my $transactions = 0;
    my $ar = $db->sql("select a.id, t.id, typeName, accountName ".
		      "from account a join accountType t on (a.type_id=t.id) ".
		      "where owner_id=? ".
		      "order by t.id", $member_id) or die "Urgh";
    while (my ($account_id, $type_id, $typeName, $accountName) = $ar->fetchrow_array) {

	my $nt = $db->sql("select id, date_part('epoch', created), source_account_id, target_account_id, amount, comment ".
			  "from accounttransaction ".
			  "where ? in (source_account_id, target_account_id) and receiptSent is null ".
			  "order by id",
			  $account_id) or die "Urgh";
	my @txn;
	while (my ($transaction_id, $ts, $source_account_id, $target_account_id, $amount, $comment) = $nt->fetchrow_array()) {
	    $amount *= -1 if $target_account_id == $account_id;
	    
	    $amount = int(100*$amount)/100;
	    $amount =~ s/\.00$//;

	    push @txn, [ strftime("%d/%m %Y - %H:%M", localtime($ts)), $amount, $comment ];
	    $transactions++;
	    
	    $db->sql("update accounttransaction set receiptSent=now() where id=?", $transaction_id) or die;
	}
	$nt->finish;	

	if (@txn) {
	    my $table = Text::Table->new("Dato", "Beløb", "Beskrivelse");
	    $table->load(@txn);

	    my $aname = ($type_id == 2 or $type_id == 3) ? $typeName : $accountName;
	    my $nye = @txn == 1 ? 'Ny transaktion' : 'Nye transaktioner';

	    my $inr = $db->sql("select sum(amount) from accountTransaction where target_account_id=?", $account_id);		
	    my ($in) = $inr->fetchrow_array;
	    $inr->finish;

	    my $outr = $db->sql("select sum(amount) from accountTransaction where source_account_id=?", $account_id);		
	    my ($out) = $outr->fetchrow_array;
	    $outr->finish;

	    my $saldo = int(100*(($out//0)-($in//0)))/100;
	    $saldo =~ s/\.00$//;

	    $accounts .= "$nye på $aname - Nuværende saldo: $saldo:\n$table\n";
	}
    }
    $ar->finish;    

    if ($accounts) {
	my ($first) = split(/ /, $realname);
	my $mail =
qq'Hej, $first, dette er en kvittering for transaktioner på din OSAA konto.

$accounts
For mere information om din konto se: https://hal.osaa.dk/hal/account/

-- 
Med venlig hilsen HAL-900, OSAAs elektronhjerne.

Open Space Aarhus
Bryggervej 30
8240 Risskov
';

	sendmail($FROM, 
		 #"$realname <dren.dk\@gmail.com>",
		 "$realname <$email>",
		 $transactions > 1 
		   ? 'Nye transaktioner på din OSAA konto'
		   : 'Ny transaktion på din OSAA konto',
		 $mail);
    }
}
$mr->finish;

$db->dbh->commit;
